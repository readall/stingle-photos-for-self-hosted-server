apply plugin: 'com.android.application'

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Loads the keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "org.c2FmZQ.photos"
        minSdkVersion 23
        targetSdkVersion 30
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Stingle Photos"
            resValue "string", "camera", "Camera"
            resValue "string", "content_provider", "org.c2FmZQ.photos.share"
            resValue "string", "api_server_url", "https://api.stingle.org/"
            resValue "string", "default_home_folder_name", "StinglePhotos"
            buildConfigField "Boolean", "ALLOW_INSECURE_TLS", 'false'
            debuggable false
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            resValue "string", "content_provider", "org.c2FmZQ.photos.share.debug"
            resValue "string", "app_name", "Stingle Photos Dev"
            resValue "string", "camera", "Camera Dev"
            resValue "string", "api_server_url", "https://apidev.stingle.org/"
            resValue "string", "default_home_folder_name", "StinglePhotosDev"
            buildConfigField "Boolean", "ALLOW_INSECURE_TLS", 'true'
            debuggable true
        }
    }
    productFlavors {
        playstore {
            dimension 'version'
        }
        fdroid {
            dimension 'version'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '21.3.6528147'
    buildToolsVersion '30.0.2'
    flavorDimensions 'version'
}

dependencies {
    implementation 'com.github.terl:lazysodium-android:4.3.1@aar'
    implementation 'net.java.dev.jna:jna:5.7.0@aar'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.3'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.7.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.7.3'
    implementation 'androidx.core:core:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation "androidx.work:work-runtime:2.6.0-beta01"
    implementation project(path: ':picasso')

    playstoreImplementation 'com.android.billingclient:billing:4.0.0'

    def camerax_version = "1.0.0-alpha06"
    def camerax_view_version = "1.0.0-alpha03"
    def camerax_ext_version = "1.0.0-alpha03"
    //noinspection GradleDependency
    implementation "androidx.camera:camera-core:$camerax_version"
    //noinspection GradleDependency
    implementation "androidx.camera:camera-camera2:$camerax_version"
    //noinspection GradleDependency
    implementation "androidx.camera:camera-view:$camerax_view_version"
    //noinspection GradleDependency
    implementation "androidx.camera:camera-extensions:$camerax_ext_version"

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.0'
}
